
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если НЕ ЗначениеЗаполнено(АвторДокумента) Тогда
		АвторДокумента = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	СуммаДокумента = Услуги.Итог("Сумма");
КонецПроцедуры


Процедура ОбработкаПроведения(Отказ, РежимПроведения)    
    Движения.ЦеныНоменклатурыПоставщиков.Записывать = Истина;
    Движения.УчётЗатрат.Записывать = Истина;
    Движения.Хозрасчетный.Записывать = Истина;
    СчетКредита = ПланыСчетов.Хозрасчетный.РасчетыСПоставщиками;
    Запрос  = Новый Запрос;
    Запрос.УстановитьПараметр("Ссылка", Ссылка);
    Запрос.Текст =
    "ВЫБРАТЬ
    |    ПоступлениеУслугУслуги.Ссылка КАК Регистратор,
    |    ПоступлениеУслуг.Дата КАК Период,
    |    ПоступлениеУслугУслуги.Номенклатура КАК Номенклатура,
    |    ПоступлениеУслугУслуги.Сумма КАК Сумма,
    |    ПоступлениеУслугУслуги.СтатьяЗатрат КАК СтатьяЗатрат,
    |    ПоступлениеУслугУслуги.Цена КАК Цена,
    |    ПоступлениеУслуг.Поставщик КАК Поставщик,
    |    ПоступлениеУслугУслуги.Номенклатура.СчетБухгалтерскогоУчета КАК СчетБухгалтерскогоУчета
    |ИЗ
    |    Документ.ПоступлениеУслуг.Услуги КАК ПоступлениеУслугУслуги
    |        ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеУслуг КАК ПоступлениеУслуг
    |        ПО ПоступлениеУслугУслуги.Ссылка = ПоступлениеУслуг.Ссылка
    |ГДЕ
    |    ПоступлениеУслуг.Ссылка = &Ссылка
    |ИТОГИ
    |    СУММА(Сумма),
    |    МАКСИМУМ(Цена)
    |ПО
    |    СтатьяЗатрат,
    |    Номенклатура";
    ВыборкаИтоги = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
    Пока ВыборкаИтоги.Следующий() Цикл
        ДвижениеЗатрат = Движения.УчётЗатрат.Добавить(); 
        ЗаполнитьЗначенияСвойств(ДвижениеЗатрат,ВыборкаИтоги); 
        ДвижениеЗатрат.Период = Дата;
        Выборка = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
        Пока Выборка.Следующий() Цикл
            Движение = Движения.ЦеныНоменклатурыПоставщиков.Добавить();
            Движение.Период = Дата;
            Движение.Номенклатура = Выборка.Номенклатура;
            Движение.Поставщик = Поставщик;
            Движение.Цена = Выборка.Цена;

            Движение = Движения.Хозрасчетный.Добавить();
            Движение.СчетДт = Выборка.СчетБухгалтерскогоУчета;
            Движение.СчетКт = СчетКредита;
            Движение.Период = Дата;
            Движение.Сумма = Выборка.Сумма;
            Движение.Содержание = "Отражено поступление услуг от поставщика";
            БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетДт, Движение.СубконтоДт, Выборка.СтатьяЗатрат);
            БухгалтерскийУчет.ЗаполнитьСубконтоПоСчету(Движение.СчетКт, Движение.СубконтоКт, Поставщик);            
        КонецЦикла;
    КонецЦикла;
КонецПроцедуры

